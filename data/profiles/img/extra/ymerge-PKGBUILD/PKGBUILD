# SPDX-License-Identifier: BSD-2-Clause

pkgname=ymerge
pkgver="0.0.5"
pkgrel=1
pkgdesc="A Source-Based Package Manager for Arch Linux."
arch=(x86_64)
url="https://github.com/flying-dude/ymerge/"
license=(BSD)
depends=(pacman fmt git abseil-cpp)
makedepends=(cmake fmt nlohmann-json ninja)
provides=(ymerge)

source=(
    "https://github.com/flying-dude/ymerge/archive/refs/tags/${pkgver}.zip"
    "https://github.com/Jorengarenar/libXDGdirs/archive/84ab4117b787c4079965c98b4578a38b63795cc0.zip"
)

b2sums=('5599d2bbf9e5d4e521f251fcb241b157fbeffa29a443dfe5dbb4f6dcb01e7e31e1268b0d19ab39b126af33cf0d2d96d99608b3b09a4f39c93044ededc6c09170'
        '816f464b468ce668cb3bf4e869e683c045a8083ec43f020df2096189a408890e41b3ec7a26b72f06cf16ed31883228f810385968162baa5c87b61cdbe37004ff')

build() {
    cp --no-target-directory --recursive libXDGdirs-* ymerge-*/src/submodules/libxdgdirs/
    cd ymerge-*

    cmake -S . -B build -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=On \
        -DYMERGE_VERSION="${pkgver}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'

    cmake --build build
}

package() {
    cd ymerge-*/build/
    DESTDIR="${pkgdir}" ninja install
}
